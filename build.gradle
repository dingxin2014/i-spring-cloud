buildscript {
    ext {
        springBootVersion = '1.5.6.RELEASE'
        springCloudVersion = 'Camden.SR6'
        dockerPluginVersion = '1.2'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("se.transmode.gradle:gradle-docker:${dockerPluginVersion}")
    }

}

configure(allprojects) { project ->
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'eclipse'
}

ext {

    moduleProjects = subprojects.findAll {
        !it.name.equals('zhaopin-build-src')
    }


}

configure(moduleProjects) { project ->

    println "Current project is ${project.name}"
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'org.springframework.boot'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }


    configurations.all {
        // Check for updates every build
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = 1.8


    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'
        options.compilerArgs += "-parameters"
    }

    repositories {
        mavenCentral()
    }

    //全局依赖
    dependencies {
        compileOnly('org.projectlombok:lombok')
        compile('org.springframework.cloud:spring-cloud-starter-eureka')
        compile('org.springframework.boot:spring-boot-starter-web')
        testCompile('org.springframework.boot:spring-boot-starter-test')
        compile('org.springframework.boot:spring-boot-starter-actuator')
    }

    project.ext.imageName = project.name
    project.ext.imageTag = project.version

    jar {
        baseName = "${project.name}"
        version = "${version}"

        manifest.attributes["Created-By"] =
                "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})"
        manifest.attributes["Implementation-Title"] = project.name
        manifest.attributes["Implementation-Version"] = project.version
    }

}

apply plugin: 'zhaopin.spring.cloud.plugin'

docker {
    repository = "daocloud.io"
    username = "being_away@qq.com"
    password = "12345qwert"
}